# Copyright 1999-2017 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2

EAPI=5

inherit readme.gentoo

DESCRIPTION="Raspberry PI boot loader and firmware"
HOMEPAGE="https://github.com/raspberrypi/firmware"
SRC_URI="https://github.com/raspberrypi/firmware/archive/${PV}.tar.gz -> ${P}.tar.gz"

LICENSE="GPL-2 raspberrypi-videocore-bin"
SLOT="0"
KEYWORDS="~arm -*"
IUSE="kernel-armv6j kernel-armv7a +dtb userland +userland_hardfp examples doc"

DEPEND=""
RDEPEND=""

S=${WORKDIR}/${P/raspberrypi-}

RESTRICT="binchecks strip"

REQUIRE_USE="userland? ( ! userland_hardfp )
	userland_hardfp? ( ! userland )
	examples? ( || ( userland userland_hardfp ) )"

pkg_preinst() {
	if ! grep "${ROOT}boot" /proc/mounts >/dev/null 2>&1; then
		ewarn "${ROOT}boot is not mounted, the files might not be installed at the right place"
	fi
}

src_configure() { :; }

src_compile() { :; }

src_install() {
	if use kernel-armv6j ; then
		insinto /boot
		doins boot/bcm2708-rpi-0-w.dtb
		doins boot/bcm2708-rpi-b-plus.dtb
		doins boot/bcm2708-rpi-b.dtb
		doins boot/bcm2708-rpi-cm.dtb
		local my_ver=$(awk '{ print $3 }' extra/uname_string)
		newins boot/kernel.img kernel-${my_ver}.img
		insinto /lib/modules
		doins -r modules/${my_ver}
		insinto /lib/modules/${my_ver}
		newins extra/Module.symvers Module-${my_ver}.symvers
		insopts -m 0600
		newins extra/System.map System-${my_ver}.map
		insopts -m 0644
	fi
	if use kernel-armv7a ; then
		insinto /boot
		doins boot/bcm2709-rpi-2-b.dtb
		doins boot/bcm2710-rpi-3-b.dtb
		doins boot/bcm2710-rpi-cm3.dtb
		local my_ver=$(awk '{ print $3 }' extra/uname_string7)
		newins boot/kernel7.img kernel-${my_ver}.img
		insinto /lib/modules
		doins -r modules/${my_ver}
		insinto /lib/modules/${my_ver}
		newins extra/Module7.symvers Module7-${my_ver}.symvers
		insopts -m 0600
		newins extra/System7.map System7-${my_ver}.map
		insopts -m 0644
	fi
	if use dtb ; then
		insinto /boot
		doins -r boot/overlays
	fi
	if use doc ; then
		dodoc -r documentation/ilcomponents
	fi
	if use userland || use userland_hardfp ; then
		cd $(usex userland_hardfp hardfp/ "")opt/vc || die
		insinto /opt/vc
		doins -r include
		into /opt
		dobin bin/*
		dobin sbin/*
		insopts -m 0755
		insinto "/opt/vc/$(get_libdir)"
		doins -r lib/*
		doenvd "${FILESDIR}"/04${PN}

		if use examples ; then
			insinto /usr/share/doc/${PF}/examples
			doins -r src/hello_pi
		fi
	fi

	newenvd "${FILESDIR}"/${PN}-0_p20130711-envd 90${PN}
	readme.gentoo_create_doc
}

DOC_CONTENTS=" config.txt and cmdline.txt need to be generated by you
More information here:
https://www.raspberrypi.org/documentation/configuration/config-txt.md
Another good source http://elinux.org/RPi_config.txt
http://elinux.org/RPi_cmdline.txt"
